rootProject.name = 'YetAnotherMessagesLibrary'

this.setupSubproject("core")

// Platforms
this.setupSubproject("platform")
this.setupSubproject("platform:bukkit", "platform-bukkit")
this.setupSubproject("platform:bungee", "platform-bungee")
this.setupSubproject("platform:velocity", "platform-velocity")

// Repositories (Data Supplier)
this.setupSubproject("repository")
this.setupSubproject("repository:okaeri", "repository-okaeri")

// Replaceable (Placeholders)
this.setupSubproject("replaceable")
this.setupSubproject("replaceable:okaeri", "replaceable-okaeri")
this.setupSubproject("replaceable:placeholderapi", "replaceable-placeholderapi")

// Code examples
this.setupSubproject("example")

def setupSubproject(String project, String name) {
    include "$project"
    findProject(":$project")?.name = name
}

def setupSubproject(String project) {
    this.setupSubproject(project, project)
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            library("spigot", "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT")
            library("bungee", "net.md-5:bungeecord-api:1.20-R0.1")
            library("velocity", "com.velocitypowered:velocity-api:3.1.1")
        }
        adventure {
            String apiVersion = "4.14.0"
            library("api", "net.kyori:adventure-api:${apiVersion}")
            library("minimessage", "net.kyori:adventure-text-minimessage:${apiVersion}")
            library("serializer-legacy", "net.kyori:adventure-text-serializer-legacy:${apiVersion}")

            String platformVersion = "4.3.0"
            library("platform-bukkit", "net.kyori:adventure-platform-bukkit:${platformVersion}")
            library("platform-bungee", "net.kyori:adventure-platform-bungeecord:${platformVersion}")
        }
        okaeri {
            library("config", "eu.okaeri:okaeri-configs-core:5.0.0-beta.6")
            library("placeholders", "eu.okaeri:okaeri-placeholders-core:4.0.7")
        }
    }
}
