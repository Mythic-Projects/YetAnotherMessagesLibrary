plugins {
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    group 'dev.peri.yetanothermessageslibrary'
    version '6.5.0'

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava.options.encoding = 'UTF-8'
}

subprojects {
    repositories {
        mavenCentral()

        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.papermc.io/repository/maven-public/" }
        maven { url "https://storehouse.okaeri.eu/repository/maven-public/" }
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    publishing {
        publications {
            libraries(MavenPublication) {
                artifactId = project.name.toLowerCase()

                from components.java

                pom {
                    url = "https://github.com/P3ridot/YetAnotherMessagesLibrary"
                    name = project.name
                    description = "YetAnotherMessagesLibrary is simple Miecraft Messages Library based on `adventure`. It allows to easily send messages to players with built-in support for localization and placeholders."

                    developers {
                        developer {
                            id = "peridot"
                            name = "Peridot"
                            email = "peridot491@pm.me"
                        }
                    }

                    scm {
                        url = "https://github.com/P3ridot/YetAnotherMessagesLibrary.git"
                        connection = "git@github.com:P3ridot/YetAnotherMessagesLibrary.git"
                        developerConnection = "git@github.com:P3ridot/YetAnotherMessagesLibrary.git"
                    }

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "http://www.opensource.org/licenses/MIT"
                            distribution = 'repo'
                        }
                    }
                }

                // Add external repositories to published artifacts
                // ~ btw: pls don't touch this
                // Original code: https://github.com/FunnyGuilds/FunnyGuilds/blob/master/build.gradle
                pom.withXml {
                    def repositories = asNode().appendNode('repositories')

                    project.getRepositories().findAll { repo ->
                        if (repo.getUrl().toString().startsWith('http')) {
                            def repository = repositories.appendNode('repository')
                            repository.appendNode('id', repo.getUrl().toString().replace("https://", "").replace("/", "-").replace(".", "-").trim())
                            repository.appendNode('url', repo.getUrl().toString())
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name "titanvale-repo"
                url "https://repo.titanvale.net/${version.toString().endsWith('SNAPSHOT') ? 'snapshots' : 'releases'}"

                credentials {
                    username = System.getenv("MAVEN_NAME")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
        }
    }
}